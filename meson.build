project('dxvk', ['c', 'cpp'], version : 'v1.10.1', meson_version : '>= 0.47', default_options : [ 'cpp_std=c++17' ])

cpu_family = target_machine.cpu_family()

cpp = meson.get_compiler('cpp')
cc = meson.get_compiler('c')
dxvk_is_msvc = cpp.get_id() == 'msvc'

compiler_args = [
  '-DNOMINMAX',
  '-D_WIN32_WINNT=0xa00',
  '-msse',
  '-msse2',
  '-msse3',
  '-mfpmath=sse',
  '-Wimplicit-fallthrough',
  # clang
  '-Wno-unused-private-field',
  '-Wno-microsoft-exception-spec',
]

link_args = [
  '-static',
  '-static-libgcc',
  '-static-libstdc++',
  # We need to set the section alignment for debug symbols to
  # work properly as well as avoiding a memcpy from the Wine loader.
  '-Wl,--file-alignment=4096',
]

# Wine's built-in back traces only work with dwarf2 symbols
if get_option('debug') and target_machine.system() == 'windows'
  compiler_args += [
    '-gstrict-dwarf',
    '-gdwarf-2',
  ]
endif

if cpu_family == 'x86' and target_machine.system() == 'windows'
  link_args += [
    '/SAFESEH:NO',
  ]
endif

if get_option('build_id')
  link_args += [
    '-Wl,--build-id',
  ]
endif

if cpu_family == 'x86'
  link_args += [
    '-Wl,--enable-stdcall-fixup',
    '-Wl,--add-stdcall-alias',
  ]
endif

add_project_arguments(cpp.get_supported_arguments(compiler_args), language: 'cpp')
add_project_arguments(cc.get_supported_arguments(compiler_args), language: 'c')
add_project_link_arguments(cpp.get_supported_link_arguments(link_args), language: 'cpp')
add_project_link_arguments(cc.get_supported_link_arguments(link_args), language: 'c')

dxvk_buildtype = get_option('buildtype')
dxvk_debug     = dxvk_buildtype == 'debug' or dxvk_buildtype == 'debugoptimized'

if dxvk_debug
else
  add_project_arguments('-DNDEBUG',   language : 'c')
endif

dxvk_include_path = include_directories('../../common/vulkan/include', './include')

if (cpu_family == 'x86_64')
  dxvk_library_path = meson.source_root() + '/lib'
else
  dxvk_library_path = meson.source_root() + '/lib32'
endif

dxvk_extradep = [ ]

#lib_vulkan  = cpp.find_library('vulkan-1', dirs : dxvk_library_path)
lib_d3d9    = cpp.find_library('d3d9')
lib_d3d11   = cpp.find_library('d3d11')
lib_dxgi    = cpp.find_library('dxgi')

if dxvk_is_msvc or host_machine.system() == 'windows'
  lib_d3dcompiler_43 = cpp.find_library('d3dcompiler')
else
  lib_d3dcompiler_43 = cpp.find_library('d3dcompiler_43', dirs : dxvk_library_path)
endif
lib_d3dcompiler_47 = cpp.find_library('d3dcompiler_47', required : false)
if not lib_d3dcompiler_47.found()
  lib_d3dcompiler_47 = cpp.find_library('d3dcompiler')
endif

exe_ext = ''
dll_ext = ''

def_spec_ext = '.def'

glsl_compiler = find_program('glslangValidator')
glsl_args = [ '-V', '--vn', '@BASENAME@', '@INPUT@', '-o', '@OUTPUT@' ]
if run_command(glsl_compiler, [ '--quiet', '--version' ], check : false).returncode() == 0
    glsl_args += [ '--quiet' ]
endif
glsl_generator = generator(glsl_compiler,
  output    : [ '@BASENAME@.h' ],
  arguments : glsl_args,
)

if dxvk_is_msvc or host_machine.system() == 'windows'
  res_ext = '.res'
  wrc = find_program('rc')
  wrc_generator = generator(wrc,
    output    : [ '@BASENAME@' + res_ext ],
    arguments : [ '/fo', '@OUTPUT@', '@INPUT@' ],
  )
else
  res_ext = '.o'
  res_args = [ '-i', '@INPUT@', '-o', '@OUTPUT@' ]
  if cpu_family == 'x86'
    res_args += [ '-F', 'pe-i386' ]
  endif
  wrc = find_program('windres')
  wrc_generator = generator(wrc,
    output    : [ '@BASENAME@' + res_ext ],
    arguments : res_args,
  )
endif

dxvk_version = vcs_tag(
  command: ['git', 'describe', '--dirty=+'],
  input:  'version.h.in',
  output: 'version.h',
)

subdir('src')

enable_tests = get_option('enable_tests')

if enable_tests
  subdir('tests')
endif
